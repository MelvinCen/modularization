apply plugin: 'com.android.application'

android {
    compileSdkVersion rootProject.ext.android["compileSdkVersion"]
    defaultConfig {
        applicationId rootProject.ext.android["applicationId"]
        minSdkVersion rootProject.ext.android["minSdkVersion"]
        targetSdkVersion rootProject.ext.android["targetSdkVersion"]
        versionCode rootProject.ext.android["versionCode"]
        versionName rootProject.ext.android["versionName"]
    }
    //用java1.8编译
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    //资源前缀
    resourcePrefix "app_"
    //app密钥配置
    signingConfigs {
        config {
            keyAlias RELEASE_KEY_ALIAS
            keyPassword RELEASE_KEY_PASSWORD
            storeFile file(RELEASE_STORE_FILE)
            storePassword RELEASE_STORE_PASSWORD
            v2SigningEnabled true//是否启用APK Signature Scheme v2
        }
    }
    buildTypes {
        debug {
            minifyEnabled false
            signingConfig signingConfigs.config
        }
        release {
            shrinkResources true // 移除无用的resource文件
            zipAlignEnabled true
            minifyEnabled true
            signingConfig signingConfigs.config
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    //源码存放地址配置,common的清单文件可以放置权限
    sourceSets {
        main {
            manifest.srcFile 'src/main/AndroidManifest.xml'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(':common')
    //根据isModule决定是否依赖业务组件
    if (!isModule.toBoolean()) {
       //如果是集成模式，则需要依赖业务模块
        implementation project(':home')
        implementation project(':list')
    }
}
